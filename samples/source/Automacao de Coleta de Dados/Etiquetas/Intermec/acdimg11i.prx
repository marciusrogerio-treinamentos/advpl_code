/*
Padrão Intermec
Programa: Img11i
Autor: Lucas Konrad França
Data: 01/06/20
Descricao: Geração de etiqueta RFID para o produto
*/
User Function Img11i()
	Local cProduto   := PARAMIXB[1]
	Local cLote      := PARAMIXB[2]
	Local cSubLote   := PARAMIXB[3]
	Local cNumSeri   := PARAMIXB[4]
	Local nQuant     := PARAMIXB[5]
	Local nCopias    := PARAMIXB[6]
	Local cAlias     := PARAMIXB[7]
	Local cChaveOrig := PARAMIXB[8]
	Local cEpc       := ""
	Local cTexto     := ""
	Local cCodBar    := AllTrim(SB1->B1_CODBAR)
	Local cTipoBar   := "MB07"
	Local nIndex     := 0

	If Len(cCodBar) == 8
		cTipoBar := 'MB03'
	ElseIf Len(cCodBar) == 13
		cTipoBar := 'MB04'
	EndIf

	For nIndex := 1 To nCopias
		cEpc := RFIDNEWEPC(cProduto, cLote, cSubLote, cNumSeri, nQuant, cAlias, cChaveOrig)
		If !Empty(cEpc)
			MSCBBEGIN(1,3)
			MSCBBOX(01,02,34,76,1)
			MSCBLineV(30,30,76,1)
			MSCBLineV(23,02,76,1)
			MSCBLineV(15,02,76,1)
			MSCBLineH(23,30,34,1)
			MSCBSAY(32,33,'PRODUTO',"R","2","01,01")
			MSCBSAY(29,33,"CODIGO","R","2","01,01")
			MSCBSAY(26,33, AllTrim(SB1->B1_COD), "R", "2", "01,01")
			MSCBSAY(22,05,"DESCRICAO","R","2","01,01")
			MSCBSAY(19,05,SB1->B1_DESC,"R", "2", "01,01")

			If !Empty(cLote)
				cTexto := "Lote: " + AllTrim(cLote)
				If !Empty(cSubLote)
					cTexto += " - " + AllTrim(cSubLote)
				EndIf
				MSCBSAY(16,05,cTexto, "R", "2", "01,01")
			EndIf

			If !Empty(cNumSeri)
				MSCBSAY(14,05,"Série: " + cNumSeri, "R", "2", "01,01")
			EndIf

			If !Empty(cCodBar)
				MSCBSAYBAR(12,22,cCodBar,"R",cTipoBar,8.36,.F.,.T.,.F.,,2,2,.F.,.F.,"1",.T.)
			EndIf

			MSCBRfid(cEpc, 768, 3, 0)
			MSCBEND()
		EndIf
	Next
Return Nil
